plugins {
	id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = project.properties['group']
archivesBaseName = project.properties['archivesBaseName']
version = project.properties['version']

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.4.12'
	implementation group: 'ma.glasnost.orika', name: 'orika-core', version: '1.5.2'
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.6'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.2'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.2'
	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

jar {
	manifest {
		attributes(
			'Created-By': "Gradle ${gradle.gradleVersion}",
			'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})"
		)
	}
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	//sign configurations.archives
	sign publishing.publications
}

publishing {
	publications {
		jar(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom {
				name = 'mapply'
				//packaging 'jar'
				// optionally artifactId can be defined here
				description = 'Annotated mapping for different beans.'
				url = 'https://github.com/Esinware/mapply'
				scm {
					connection = 'scm:git:git:github.com/Esinware/mapply.git'
					developerConnection = 'scm:git:ssh:github.com:Esinware/mapply.git'
					url = 'https://github.com/Esinware/mapply'
				}
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'raffaele'
						name = 'Raffaele Specia'
						email = 'r.specia@esinware.it'
					}
					developer {
						id = 'manuel'
						name = 'Manuel Corini'
						email = 'm.corini@esinware.it'
					}
				}
			}
		}
	}
}

nexusPublishing {
	repositories {
		myNexus {
			nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
			snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
			username = project.properties['oss.username']
			password = project.properties['oss.password']
		}
	}
}
